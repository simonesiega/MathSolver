Grammatica utilizzata per la rappresentazione e il parsing di espressioni aritmetiche

Questa grammatica definisce le regole per la sintassi di espressioni aritmetiche che supportano:
- Numeri decimali con segno
- Operazioni binarie: somma, sottrazione, moltiplicazione, divisione
- Moltiplicazione implicita (es. 2(3+4) → 2 * (3+4))
- Potenze (a^b) e radici ennesime (a$b)
- Parentesi annidate

========================
GRAMMATICA FORMALE (CFG)
========================

- evaluate
F  → E "="                 

- evaluate_e
E  → P E'
                   
- evaluate_e_prime
E' → "+" P E'               
    | "−" P E'
    | ε

- evaluate_p
P  → U P'
                   
- evaluate_p_prime
P' → "*" U P'               
    | "/" U P'
    | ImplicitMult U P'      - solo in alcuni casi
    | ε

ImplicitMult → 
    | if_last_token_is_closing_paren_and_next_is_open_paren
    | if_last_token_is_number_and_next_is_open_paren
    | if_last_token_is_closing_paren_and_next_is_number

- evaluate_u
U  → B U'   
                
- evaluate_u_prime
U' → "^" U                  - evaluate_exponentiation
    | "$" U                 - evaluate_root
    | ε

B  → "−" B
    | unsigned number
    | "(" E ")"

========================
DESCRIZIONE DEI SIMBOLI
========================

F (Formula): espressione aritmetica completa, che deve terminare con il simbolo "="
Esempio: (2 + 3) * 4 =

E (Espressione): gestisce somma e sottrazione tra blocchi (+, −), con precedenza minore
→ Combinazione ricorsiva di termini P

P (Prodotto): gestisce moltiplicazione (*), divisione (/) e moltiplicazione implicita
→ Esempio: 2 * 3, 2(3+1), (1+2)(4-1)

U (Unità): gestisce le potenze (^) e le radici ennesime ($), con associatività a destra
→ Esempio: 2^3, 27$3

B (Base): rappresenta un valore elementare
→ Può essere un numero (unsigned number), una negazione (−), o un’espressione tra parentesi

========================
ESEMPI VALIDI
========================

Espressione                                             → Interpretazione
-------------------------------------------------------------------------
2 + 3 * 4 =                                            → 2 + (3 * 4) = 14
-5.3 + 2 =                                             → -5.3 + 2 = -3.3
2(3 + 1) =                                             → 2 * (3 + 1) = 8
(1 + 2)(3 + 4) =                                       → 3 * 7 = 21
(.12)(1*9/2.3) =                                       → 0.12 * (9 / 2.3)
2^3 =                                                  → 8
27$3 =                                                 → 3 (radice cubica di 27)
4^2 $ 2 =                                              → 4

((8 - 9.81 * 3.14) - .12(1*9/2.3) + -5.17) =           Raggruppa i casi precedentemente citati 
                                  

========================
ESEMPI NON VALIDI
========================

Espressione           → Errore
----------------------------------------
2 + =                 → Manca termine dopo +
2 * (3 + 4            → Parentesi non chiusa
5 5 =                 → Ambiguo: due numeri consecutivi
2 ^ =                 → Manca esponente
27 $ =                → Manca indice radice

========================
NOTE FINALI
========================

- La moltiplicazione implicita è trattata solo nel contesto del prodotto P.
- Le potenze (^) e radici ($) sono gestite tramite U, con precedenza maggiore rispetto ai prodotti.
- Il simbolo '=' serve come delimitatore di fine input e non fa parte del calcolo.
- L’estensione a funzioni(sin, log) o costanti(e) è possibile con produzioni aggiuntive.
